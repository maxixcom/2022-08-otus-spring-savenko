plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

group = 'io.github.maxixcom.otus'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('mockitoVersion', "4.8.0")
    set('assertjVersion', "3.23.1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework.data:spring-data-rest-hal-explorer'

    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    testImplementation 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}

application {
    mainClass = 'io.github.maxixcom.otus.booklib.BooklibApplication'
}


gitProperties {
    extProperty = 'gitProps' // git properties will be put in a map at project.ext.gitProps
}

jib{
    from {
        image="openjdk:17-jdk"
    }
    to {
        image= project.provider { "jib/${project.name}:" + project.ext.gitProps['git.branch']}
    }
    container {
        ports = ['8080']
        creationTime = project.provider { project.ext.gitProps['git.commit.time'] }
    }
}
